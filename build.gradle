plugins {
    id 'java'
    id 'kr.entree.spigradle' version '1.2.1'
}

def versionNumber = "$System.env.X_VERSION"

group 'com.xethlyx.plugins'
version versionNumber

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
}

spigot {
    name = 'XEnchant'
    version = versionNumber
    authors = ['github.com/xethlyx']
    apiVersion = '1.17'
    load = STARTUP
    commands {
        xenchant {
            aliases = ['xench']
            description = 'Enchant a tool with a custom enchant.'
            permission = 'xenchant.command.enchant'
            permissionMessage = 'You do not have permission!'
            usage = '/<command> [enchantment] [level]'
        }
    }
    permissions {
        'xenchant.command.enchant' {
            description = 'Permission for /xenchant command.'
            defaults = 'op'
        }
        'xenchant.command.*' {
            description = 'Wildcard permission for all enchants'
            defaults = 'op'
            children = ['xenchant.command.enchant': true]
        }
    }
}

task devBuild {
    group "dev"
    description "Build and then load onto dev server"
    doLast {
        copy {
            from 'build/libs'
            into 'server/plugins'
            include '*.jar'
        }
    }
}

devBuild.dependsOn jar